plugins {
    id 'java'
    id("org.springframework.boot") version "3.4.3"
    id("io.spring.dependency-management") version "1.1.7"

    id 'au.com.dius.pact' version '4.6.17'
}

group = 'com.dijure'
version = '3.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

/**
 * The task pactVerify will verify the contracts.
 * Before running this task, set the following environment variables:
 *   export PACT_BROKER_URL=http://localhost:30111  # URL to your Pact Broker
 *   export PROVIDER_HOST=localhost                 # Host where your provider service is running
 *   export PROVIDER_PORT=30103                     # Port where your provider service is running
 *
 * Then run: ./gradlew pactVerify
 *
 * These variables default to localhost with standard ports if not specified.
 */
pact {
    broker {
        // Get the broker URL from an environment variable or default to localhost
        pactBrokerUrl = System.getenv('PACT_BROKER_URL') ?: 'http://localhost:30111'
    }

    serviceProviders {
        'Aggregator' {
            // Get provider details from environment variables
            protocol = 'http'
            host = System.getenv('PROVIDER_HOST') ?: 'localhost'
            port = Integer.parseInt(System.getenv('PROVIDER_PORT') ?: '30103')
            path = '/'

            fromPactBroker {
                selectors = ['tag:master']
            }
        }
    }
}